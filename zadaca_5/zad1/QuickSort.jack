class QuickSort
{
    function void Swap (Array A, int i, int j)
    {
        var int temp;
        let temp = A[i];
        let A[i] = A[j];
        let A[j] = temp;
        return;
    }

    function int partition (Array A, int low, int high)
    {
        var int pivot, i, j;

        let pivot = A[high];
        let i = low - 1;
        let j = low;

        while(j < high)
        {
            if(A[j] < pivot)
            {
                let i = i + 1;
                do QuickSort.Swap(A, i, j);
            }

            let j = j + 1;
        }

        do QuickSort.Swap(A, i + 1, high);

        return (i + 1);
    }

    function void Sort (Array A, int low, int high)
    {
        var int x;

        if (low < high)
        {
            let x = QuickSort.partition(A, low, high);

            do QuickSort.Sort(A, low, x - 1);
            do QuickSort.Sort(A, x + 1, high);
        }
        return;
    }

}